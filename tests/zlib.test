# Test zlib usage                                                    -*-tcl-*-
#
# $Id$

# Override zlib
if {[catch {package require zlib}]} {
    package provide zlib 1.0
    package require zlib
}
source [file join [file dirname [file join [pwd] [info script]]] init.tcl]
proc zlib {args} {set ::zlibcalled 1 ; return "zlib_dummy"}

test zlib-1.1 {compress flag} -body {
    set res {}
    lappend res [catch {pdf4tcl::new apa -compress urga}]
    pdf4tcl::new apa -compress 0
    apa destroy
    pdf4tcl::new apa -compress 1
    set res
} -result {1} -cleanup {apa destroy}

test zlib-1.2 {zlib used} -body {
    set ::zlibcalled 0
    set res {}
    pdf4tcl::new apa -compress 1
    lappend res $::zlibcalled
    apa startPage
    lappend res $::zlibcalled
    apa endPage
    lappend res $::zlibcalled
    set res
} -result {0 0 1} -cleanup {apa destroy}


# No zlib
catch {::_instrument_::cleanup}
package forget zlib
package forget pdf4tcl
rename zlib {}
source [file join [file dirname [file join [pwd] [info script]]] init.tcl]
proc zlib {args} {set ::zlibcalled 1 ; return "zlib_dummy"}

test zlib-2.1 {compress flag} -body {
    set res {}
    lappend res [catch {pdf4tcl::new apa -compress urga}]
    pdf4tcl::new apa -compress 0
    apa destroy
    pdf4tcl::new apa -compress 1
    set res
} -result {1} \
        -errorOutput "Package zlib not available. Sorry, no compression.\n" \
        -cleanup {apa destroy}

test zlib-2.2 {zlib not used} -body {
    set ::zlibcalled 0
    set res {}
    pdf4tcl::new apa -compress 1
    lappend res $::zlibcalled
    apa startPage
    lappend res $::zlibcalled
    apa endPage
    lappend res $::zlibcalled
    set res
} -result {0 0 0} \
        -errorOutput "Package zlib not available. Sorry, no compression.\n" \
        -cleanup {apa destroy}

::tcltest::cleanupTests
